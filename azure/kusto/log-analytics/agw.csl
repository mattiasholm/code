// List all requests, ordered by time generated:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog'
| order by TimeGenerated
| project ['Time generated'] = TimeGenerated, ['Client IP'] = clientIP_s, ['Host header'] = host_s, ['Request path'] = requestUri_s, ['Query strings'] = requestQuery_s, ['HTTP status'] = httpStatus_d, ['HTTP method'] = httpMethod_s, ['HTTP version'] = httpVersion_s, ['TLS version'] = sslProtocol_s, ['TLS cipher'] = sslCipher_s, ['Rule name'] = ruleName_s, ['Instance'] = instanceId_s, ['User agent'] = userAgent_s, ['Client port'] = clientPort_d, ['Duration'] = timeTaken_d, ['Request size'] = receivedBytes_d, ['Response size'] = sentBytes_d

// List only successful requests, ordered by time generated:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog' and httpStatus_d == 200
| order by TimeGenerated
| project ['Time generated'] = TimeGenerated, ['Client IP'] = clientIP_s, ['Host header'] = host_s, ['Request path'] = requestUri_s, ['Query strings'] = requestQuery_s, ['HTTP status'] = httpStatus_d, ['HTTP method'] = httpMethod_s, ['HTTP version'] = httpVersion_s, ['TLS version'] = sslProtocol_s, ['TLS cipher'] = sslCipher_s, ['Rule name'] = ruleName_s, ['Instance'] = instanceId_s, ['User agent'] = userAgent_s, ['Client port'] = clientPort_d, ['Duration'] = timeTaken_d, ['Request size'] = receivedBytes_d, ['Response size'] = sentBytes_d

// List only failed requests, ordered by time generated:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog' and httpStatus_d >= 400
| order by TimeGenerated
| project ['Time generated'] = TimeGenerated, ['Client IP'] = clientIP_s, ['Host header'] = host_s, ['Request path'] = requestUri_s, ['Query strings'] = requestQuery_s, ['HTTP status'] = httpStatus_d, ['HTTP method'] = httpMethod_s, ['HTTP version'] = httpVersion_s, ['TLS version'] = sslProtocol_s, ['TLS cipher'] = sslCipher_s, ['Rule name'] = ruleName_s, ['Instance'] = instanceId_s, ['User agent'] = userAgent_s, ['Client port'] = clientPort_d, ['Duration'] = timeTaken_d, ['Request size'] = receivedBytes_d, ['Response size'] = sentBytes_d

// List only requests from a specific IP, ordered by time generated:
let ip = '127.0.0.1';
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog' and clientIP_s == ip
| order by TimeGenerated
| project ['Time generated'] = TimeGenerated, ['Client IP'] = clientIP_s, ['Host header'] = host_s, ['Request path'] = requestUri_s, ['Query strings'] = requestQuery_s, ['HTTP status'] = httpStatus_d, ['HTTP method'] = httpMethod_s, ['HTTP version'] = httpVersion_s, ['TLS version'] = sslProtocol_s, ['TLS cipher'] = sslCipher_s, ['Rule name'] = ruleName_s, ['Instance'] = instanceId_s, ['User agent'] = userAgent_s, ['Client port'] = clientPort_d, ['Duration'] = timeTaken_d, ['Request size'] = receivedBytes_d, ['Response size'] = sentBytes_d

// Count total number of requests:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog'
| summarize count()
| project ['Number of requests'] = count_

// Count number of successful requests per API, ordered by count:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog' and httpStatus_d == 200
| summarize count() by api=extract('^(/[^/]*/)', 0, requestUri_s)
| order by count_
| project API = api, ['Number of requests'] = count_

// Render a time chart of all requests:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog'
| summarize ['Number of requests'] = count() by bin(TimeGenerated, 1m)
| render timechart

// Render a time chart of successful requests, grouped by API:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog' and httpStatus_d == 200
| summarize ['Number of requests'] = count() by api=extract('^(/[^/]*/)', 0, requestUri_s), bin(TimeGenerated, 1m)
| render timechart

// List most common HTTP errors:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog' and httpStatus_d >= 400
| summarize count() by httpStatus_d
| order by count_
| project ['HTTP status'] = httpStatus_d, ['Number of requests'] = count_

// List most common HTTP errors and the corresponding request path:
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog' and httpStatus_d >= 400
| summarize count() by httpStatus_d, requestUri_s
| order by count_
| project ['HTTP status'] = httpStatus_d, ['Number of requests'] = count_, ['Request path'] = requestUri_s

// Summarize a specific HTTP status by request path, ordered by count:
let httpStatus = 404;
AzureDiagnostics
| where Category == 'ApplicationGatewayAccessLog' and httpStatus_d == httpStatus
| summarize count() by requestUri_s
| order by count_
| project ['Number of requests'] = count_, ['Request path'] = requestUri_s
